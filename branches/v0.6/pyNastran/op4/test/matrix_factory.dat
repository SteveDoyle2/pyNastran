ASSIGN OUTPUT4='sample.op4' UNIT=11
$
$ This DMAP alter generates a bunch of matrices for testing
$
SOL 100 $
COMPILE USERDMAP LIST $
ALTER 2 $
$
TYPE PARM,,LOGICAL,Y,,NPATRN=TRUE $
TYPE PARM,,LOGICAL,Y,,NIDENT=TRUE $
TYPE PARM,,LOGICAL,Y,,NRANDM=TRUE $
TYPE PARM,,LOGICAL,Y,,NCMPLX=TRUE $
TYPE PARM,,CHAR8,Y,OFMT=DMIGPCH   $ Output format
TYPE PARM,NDDL,I,N,OMAXR          $ NDDL Maximum record size for OUTPUT2 files
$
GP1 GEOM1,GEOM2,GEOM3/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/
                     S,N,NOCSTM/S,N,NOPOINTS $
$
IF (NPATRN) THEN $
   MESSAGE //'GENERATING'/LUSET/' X'/LUSET/' PATTERN MATRIX' $
   MATGEN ,/PATRN/4/LUSET/LUSET//LUSET/1/1 $
ENDIF $
$
IF (NIDENT) THEN $
   MESSAGE //'GENERATING'/LUSET/' X'/LUSET/' IDENTITY MATRIX' $
   MATGEN ,/IDENT/1/LUSET $
ENDIF $
$
IF (NRANDM) THEN $
   MESSAGE //'GENERATING'/LUSET/' X'/LUSET/' RANDOM MATRIX' $
   MATGEN ,/RANDM/5/LUSET/LUSET//1 $
ENDIF $
$
IF (NCMPLX) THEN $
   MESSAGE //'GENERATING'/LUSET/' X          50 COMPLEX MATRIX' $
   MATGEN ,/REAL/5/50/LUSET//2 $
   MATGEN ,/IMAG/5/50/LUSET//3 $
   ADD    REAL,IMAG/CMPLX/(1.0, 0.0)/(0.0, 1.0) $
ENDIF $
$
IF ( OFMT='DMIGPCH' ) THEN $
   MESSAGE //'WRITING IN DMIG FORMAT TO PCH FILE' $
   MATMOD PATRN,EQEXIN/MPATRN,/16/1 $
   MATMOD IDENT,EQEXIN/MIDENT,/16/1 $
   MATMOD RANDM,EQEXIN/MRANDM,/16/1 $
   MATMOD CMPLX,EQEXIN/MCMPLX,/16/1 $
ELSE IF ( OFMT='DMIPCH' ) THEN $
   MESSAGE //'WRITING IN DMI FORMAT TO PCH FILE' $
   MATPCH PATRN,IDENT,RANDM,CMPLX// $
ELSE IF ( OFMT='DMIGOP2' ) THEN $
   MESSAGE //'WRITING IN DMIG FORMAT TO OP2 FILE' $
   MATMOD PATRN,EQEXIN/MPATRN,/16 $
   MATMOD IDENT,EQEXIN/MIDENT,/16 $
   MATMOD RANDM,EQEXIN/MRANDM,/16 $
   MATMOD CMPLX,EQEXIN/MCMPLX,/16 $
   OUTPUT2 MPATRN,MIDENT,MRANDM,MCMPLX//-1/11//OMAXR/'MATRIX'/'MATRIX'/
                                                     'MATRIX'/'MATRIX' $
ELSE IF ( OFMT='MATOP2' ) THEN $
   MESSAGE //'WRITING IN MATRIX FORMAT TO OP2 FILE' $
   OUTPUT2 PATRN,IDENT,RANDM,CMPLX//-1/11//OMAXR/'MATRIX'/'MATRIX'/
                                                 'MATRIX'/'MATRIX' $
ELSE IF ( OFMT='MATOP4' ) THEN $
   MESSAGE //'WRITING IN MATRIX FORMAT TO OP4 FILE' $
   OUTPUT4 PATRN,IDENT,RANDM,CMPLX//-1/11///16 $
ELSE $
   MESSAGE //'ERROR - UNRECOGNIZED OUTPUT FORMAT' $
ENDIF $
$
CEND
BEGIN BULK
$
$ Choose output format by uncommenting appropriate value
$
$PARAM,OFMT,DMIGPCH $ DMIG OUTPUT FORMAT TO PCH FILE
PARAM,OFMT,DMIPCH  $ DMI OUTPUT FORMAT TO PCH FILE
$PARAM,OFMT,DMIGOP2 $ DMIG OUTPUT FORMAT TO OP2 FILE
$PARAM,OFMT,MATOP2  $ MATRIX OUTPUT2 FORMAT
$PARAM,OFMT,MATOP4  $ MATRIX OUTPUT4 FORMAT (FORMAT ON ASSIGN)
$
$ Uncomment logical=FALSE to turn off specific matrices (default is TRUE)
$
$PARAM,NPATRN,FALSE
$PARAM,NIDENT,FALSE
$PARAM,NRANDM,FALSE
$PARAM,NCMPLX,FALSE
$
$ Size of matrices is governed by number of DOF, which is 
$ controlled by number of SPOINTS
$
SPOINT,1,THRU,2000
$
ENDDATA
